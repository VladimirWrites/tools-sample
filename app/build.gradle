apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.vlad1m1r.toolssample"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
}

clean.doFirst {
    println "cleanSamples"
    def samplesDir = new File(projectDir.absolutePath, "sampledata")
    if (samplesDir.exists()) {
        samplesDir.deleteDir()
    }
}

preBuild.doFirst {
    println "fetchSamplesStarted"
    def samplesDir = new File(projectDir.absolutePath, "sampledata")
    if (samplesDir.exists()) {
        println "samples dir already exists"
        return
    }
    samplesDir.mkdir()

    def files = ["lotr.json", "starwars.json"]
    for(fileName in files) {
        def names = new File(samplesDir, fileName)

        new URL("https://raw.githubusercontent.com/vlad1m1r990/tools-sample/master/sampledata/$fileName").withInputStream { i ->
            names.withOutputStream {
                it << i
            }
        }
        println "fetchSample: $fileName"
    }
    println "fetchSamplesEnded"
}